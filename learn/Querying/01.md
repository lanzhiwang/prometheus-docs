# QUERYING PROMETHEUS

* https://prometheus.io/docs/prometheus/latest/querying/basics/

Prometheus provides a functional query language called PromQL (Prometheus Query Language) that lets the user select and aggregate time series data in real time. The result of an expression can either be shown as a graph, viewed as tabular data in Prometheus's expression browser, or consumed by external systems via the [HTTP API](https://prometheus.io/docs/prometheus/latest/querying/api/).
Prometheus 提供了一种名为 PromQL（Prometheus Query Language）的功能查询语言，可以让用户实时选择和聚合时间序列数据。 表达式的结果可以显示为图表，在 Prometheus 表达式浏览器中以表格数据形式查看，或者由外部系统通过 HTTP API 使用。

## Examples

This document is meant as a reference. For learning, it might be easier to start with a couple of [examples](https://prometheus.io/docs/prometheus/latest/querying/examples/).
本文档仅供参考。 对于学习来说，从几个例子开始可能会更容易。

## Expression language data types

In Prometheus's expression language, an expression or sub-expression can evaluate to one of four types:
在 Prometheus 的表达式语言中，表达式或子表达式可以计算为以下四种类型之一：

- **Instant vector** - a set of time series containing a single sample for each time series, all sharing the same timestamp
  一组时间序列，每个时间序列包含一个样本，所有时间序列共享相同的时间戳

- **Range vector** - a set of time series containing a range of data points over time for each time series
  一组时间序列，其中包含每个时间序列随时间变化的一系列数据点

- **Scalar** - a simple numeric floating point value
  一个简单的浮点值

- **String** - a simple string value; currently unused
  一个简单的字符串值； 目前未使用

Depending on the use-case (e.g. when graphing vs. displaying the output of an expression), only some of these types are legal as the result from a user-specified expression. For example, an expression that returns an instant vector is the only type that can be directly graphed.
根据用例（例如，当绘制图表或显示表达式的输出时），只有其中一些类型作为用户指定表达式的结果是合法的。 例如，返回即时向量的表达式是唯一可以直接绘制图形的类型。

> Notes about the experimental native histograms:
> 关于实验本机直方图的注释：
>
> - Ingesting native histograms has to be enabled via a [feature flag](https://prometheus.io/docs/prometheus/feature_flags/#native-histograms).
>   必须通过功能标志启用摄取本机直方图。
>
>- Once native histograms have been ingested into the TSDB (and even after disabling the feature flag again), both instant vectors and range vectors may now contain samples that aren't simple floating point numbers (float samples) but complete histograms (histogram samples). A vector may contain a mix of float samples and histogram samples.
> 一旦本机直方图被引入 TSDB（甚至在再次禁用特征标志之后），即时向量和范围向量现在可能包含的样本不是简单的浮点数（浮点样本），而是完整的直方图（直方图样本）。 向量可以包含浮点样本和直方图样本的混合。

## Literals

### String literals

Strings may be specified as literals in single quotes, double quotes or backticks.
字符串可以指定为单引号、双引号或反引号中的文字。

PromQL follows the same [escaping rules as Go](https://golang.org/ref/spec#String_literals). In single or double quotes a backslash begins an escape sequence, which may be followed by `a`, `b`, `f`, `n`, `r`, `t`, `v` or `\`. Specific characters can be provided using octal (`\nnn`) or hexadecimal (`\xnn`, `\unnnn` and `\Unnnnnnnn`).
PromQL 遵循与 Go 相同的转义规则。 在单引号或双引号中，反斜杠开始转义序列，后面可以跟 a、b、f、n、r、t、v 或 \。 可以使用八进制 (\nnn) 或十六进制（\xnn、\unnnn 和 \Unnnnnnnn）提供特定字符。

No escaping is processed inside backticks. Unlike Go, Prometheus does not discard newlines inside backticks.
反引号内不处理转义。 与 Go 不同，Prometheus 不会丢弃反引号内的换行符。

Example:

```
"this is a string"
'these are unescaped: \n \\ \t'
`these are not unescaped: \n ' " \t`
```

### Float literals

Scalar float values can be written as literal integer or floating-point numbers in the format (whitespace only included for better readability):
标量浮点值可以按以下格式写入文字整数或浮点数（仅包含空格是为了提高可读性）：

```
[-+]?(
      [0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?
    | 0[xX][0-9a-fA-F]+
    | [nN][aA][nN]
    | [iI][nN][fF]
)
```

Examples:

```
23
-2.43
3.4e-9
0x8f
-Inf
NaN
```

## Time series Selectors

### Instant vector selectors

Instant vector selectors allow the selection of a set of time series and a single sample value for each at a given timestamp (instant): in the simplest form, only a metric name is specified. This results in an instant vector containing elements for all time series that have this metric name.
即时向量选择器允许在给定时间戳（即时）选择一组时间序列和每个时间序列的单个样本值：在最简单的形式中，仅指定度量名称。 这会产生一个即时向量，其中包含具有此指标名称的所有时间序列的元素。

This example selects all time series that have the `http_requests_total` metric name:
此示例选择具有 http_requests_total 指标名称的所有时间序列：

```
http_requests_total
```

It is possible to filter these time series further by appending a comma separated list of label matchers in curly braces (`{}`).
可以通过在大括号 ({}) 中附加逗号分隔的标签匹配器列表来进一步过滤这些时间序列。

This example selects only those time series with the `http_requests_total` metric name that also have the `job` label set to `prometheus` and their `group` label set to `canary`:
此示例仅选择那些具有 http_requests_total 指标名称且作业标签设置为 prometheus 且组标签设置为 canary 的时间序列：

```
http_requests_total{job="prometheus",group="canary"}
```

It is also possible to negatively match a label value, or to match label values against regular expressions. The following label matching operators exist:
还可以对标签值进行负匹配，或者将标签值与正则表达式进行匹配。 存在以下标签匹配运算符：

- `=`: Select labels that are exactly equal to the provided string.
  选择与提供的字符串完全相同的标签。

- `!=`: Select labels that are not equal to the provided string.
  选择不等于提供的字符串的标签。

- `=~`: Select labels that regex-match the provided string.
  选择与提供的字符串正则表达式匹配的标签。

- `!~`: Select labels that do not regex-match the provided string.
  选择与提供的字符串正则表达式不匹配的标签。

Regex matches are fully anchored. A match of `env=~"foo"` is treated as `env=~"^foo$"`.
正则表达式匹配是完全锚定的。 env=~"foo" 的匹配被视为 env=~"^foo$"。

For example, this selects all `http_requests_total` time series for `staging`, `testing`, and `development` environments and HTTP methods other than `GET`.
例如，这会选择用于暂存、测试和开发环境的所有 http_requests_total 时间序列以及除 GET 之外的 HTTP 方法。

```
http_requests_total{environment=~"staging|testing|development", method!="GET"}
```

Label matchers that match empty label values also select all time series that do not have the specific label set at all. It is possible to have multiple matchers for the same label name.
匹配空标签值的标签匹配器还会选择根本没有特定标签集的所有时间序列。 同一标签名称可以有多个匹配器。

Vector selectors must either specify a name or at least one label matcher that does not match the empty string. The following expression is illegal:
向量选择器必须指定一个名称或至少一个与空字符串不匹配的标签匹配器。 以下表达式是非法的：

```
{job=~".*"} # Bad!
```

In contrast, these expressions are valid as they both have a selector that does not match empty label values.

```
{job=~".+"}              # Good!
{job=~".*",method="get"} # Good!
```

Label matchers can also be applied to metric names by matching against the internal `__name__` label. For example, the expression `http_requests_total` is equivalent to `{__name__="http_requests_total"}`. Matchers other than `=` (`!=`, `=~`, `!~`) may also be used. The following expression selects all metrics that have a name starting with `job:`:
标签匹配器还可以通过匹配内部 `__name__`` 标签来应用于指标名称。 例如，表达式 http_requests_total 相当于 `{__name__="http_requests_total"}`。 也可以使用 = (!=, =~, !~) 以外的匹配器。 以下表达式选择名称以 job: 开头的所有指标：

```
{__name__=~"job:.*"}
```

The metric name must not be one of the keywords `bool`, `on`, `ignoring`, `group_left` and `group_right`. The following expression is illegal:

```
on{} # Bad!
```

A workaround for this restriction is to use the `__name__` label:

```
{__name__="on"} # Good!
```

All regular expressions in Prometheus use [RE2 syntax](https://github.com/google/re2/wiki/Syntax).

### Range Vector Selectors

Range vector literals work like instant vector literals, except that they select a range of samples back from the current instant. Syntactically, a [time duration](https://prometheus.io/docs/prometheus/latest/querying/basics/#time-durations) is appended in square brackets (`[]`) at the end of a vector selector to specify how far back in time values should be fetched for each resulting range vector element. The range is a closed interval, i.e. samples with timestamps coinciding with either boundary of the range are still included in the selection.
范围向量文字的工作方式与即时向量文字类似，只不过它们选择从当前时刻返回的一系列样本。 从语法上讲，持续时间附加在向量选择器末尾的方括号 ([]) 中，以指定应为每个结果范围向量元素提取多早的时间值。 该范围是一个闭区间，即时间戳与该范围的任一边界一致的样本仍然包含在选择中。

In this example, we select all the values we have recorded within the last 5 minutes for all time series that have the metric name `http_requests_total` and a `job` label set to `prometheus`:
在此示例中，我们选择过去 5 分钟内为指标名称为 http_requests_total 且作业标签设置为 prometheus 的所有时间序列记录的所有值：

```
http_requests_total{job="prometheus"}[5m]
```

### Time Durations

Time durations are specified as a number, followed immediately by one of the following units:

- `ms` - milliseconds
- `s` - seconds
- `m` - minutes
- `h` - hours
- `d` - days - assuming a day has always 24h
- `w` - weeks - assuming a week has always 7d
- `y` - years - assuming a year has always 365d

Time durations can be combined, by concatenation. Units must be ordered from the longest to the shortest. A given unit must only appear once in a time duration.
可以通过串联来组合持续时间。 单位必须按照从最长到最短的顺序排列。 给定单位在一段时间内只能出现一次。

Here are some examples of valid time durations:
以下是有效持续时间的一些示例：

```
5h
1h30m
5m
10s
```

### Offset modifier

The `offset` modifier allows changing the time offset for individual instant and range vectors in a query.
偏移修饰符允许更改查询中各个瞬时和范围向量的时间偏移。

For example, the following expression returns the value of `http_requests_total` 5 minutes in the past relative to the current query evaluation time:
例如，以下表达式返回相对于当前查询评估时间过去 5 分钟的 http_requests_total 值：

```
http_requests_total offset 5m
```

Note that the `offset` modifier always needs to follow the selector immediately, i.e. the following would be correct:

```
sum(http_requests_total{method="GET"} offset 5m) // GOOD.
```

While the following would be *incorrect*:

```
sum(http_requests_total{method="GET"}) offset 5m // INVALID.
```

The same works for range vectors. This returns the 5-minute rate that `http_requests_total` had a week ago:

```
rate(http_requests_total[5m] offset 1w)
```

For comparisons with temporal shifts forward in time, a negative offset can be specified:
为了与时间上的时间向前移动进行比较，可以指定负偏移：

```
rate(http_requests_total[5m] offset -1w)
```

Note that this allows a query to look ahead of its evaluation time.

### @ modifier

The `@` modifier allows changing the evaluation time for individual instant and range vectors in a query. The time supplied to the `@` modifier is a unix timestamp and described with a float literal.
@ 修饰符允许更改查询中各个瞬时向量和范围向量的评估时间。 提供给@修饰符的时间是unix时间戳并用浮点文字描述。

For example, the following expression returns the value of `http_requests_total` at `2021-01-04T07:40:00+00:00`:
例如，以下表达式返回 2021-01-04T07:40:00+00:00 处的 http_requests_total 值：

```
http_requests_total @ 1609746000
```

Note that the `@` modifier always needs to follow the selector immediately, i.e. the following would be correct:

```
sum(http_requests_total{method="GET"} @ 1609746000) // GOOD.
```

While the following would be *incorrect*:

```
sum(http_requests_total{method="GET"}) @ 1609746000 // INVALID.
```

The same works for range vectors. This returns the 5-minute rate that `http_requests_total` had at `2021-01-04T07:40:00+00:00`:

```
rate(http_requests_total[5m] @ 1609746000)
```

The `@` modifier supports all representation of float literals described above within the limits of `int64`. It can also be used along with the `offset` modifier where the offset is applied relative to the `@` modifier time irrespective of which modifier is written first. These 2 queries will produce the same result.
@ 修饰符支持上述 int64 限制内的所有浮点文字表示形式。 它还可以与 offset 修饰符一起使用，其中偏移量相对于 @ 修饰符时间应用，而不管首先写入哪个修饰符。 这 2 个查询将产生相同的结果。

```
# offset after @
http_requests_total @ 1609746000 offset 5m
# offset before @
http_requests_total offset 5m @ 1609746000
```

Additionally, `start()` and `end()` can also be used as values for the `@` modifier as special values.
此外，start() 和 end() 也可以用作 @ 修饰符的特殊值。

For a range query, they resolve to the start and end of the range query respectively and remain the same for all steps.
对于范围查询，它们分别解析为范围查询的开始和结束，并且对于所有步骤保持相同。

For an instant query, `start()` and `end()` both resolve to the evaluation time.
对于即时查询，start() 和 end() 都解析为评估时间。

```
http_requests_total @ start()
rate(http_requests_total[5m] @ end())
```

Note that the `@` modifier allows a query to look ahead of its evaluation time.

## Subquery

Subquery allows you to run an instant query for a given range and resolution. The result of a subquery is a range vector.
子查询允许您针对给定范围和分辨率运行即时查询。 子查询的结果是范围向量。

Syntax: `<instant_query> '[' <range> ':' [<resolution>] ']' [ @ <float_literal> ] [ offset <duration> ]`

- `<resolution>` is optional. Default is the global evaluation interval.

## Operators

Prometheus supports many binary and aggregation operators. These are described in detail in the [expression language operators](https://prometheus.io/docs/prometheus/latest/querying/operators/) page.
Prometheus 支持许多二元和聚合运算符。 这些在表达式语言运算符页面中有详细描述。

## Functions

Prometheus supports several functions to operate on data. These are described in detail in the [expression language functions](https://prometheus.io/docs/prometheus/latest/querying/functions/) page.
Prometheus 支持多种对数据进行操作的函数。 这些在表达式语言函数页面中有详细描述。

## Comments

PromQL supports line comments that start with `#`. Example:

```
    # This is a comment
```

## Gotchas
陷阱

### Staleness
陈旧性

When queries are run, timestamps at which to sample data are selected independently of the actual present time series data. This is mainly to support cases like aggregation (`sum`, `avg`, and so on), where multiple aggregated time series do not exactly align in time. Because of their independence, Prometheus needs to assign a value at those timestamps for each relevant time series. It does so by simply taking the newest sample before this timestamp.
运行查询时，将独立于实际存在的时间序列数据来选择采样数据的时间戳。 这主要是为了支持聚合（总和、平均值等）等情况，其中多个聚合时间序列在时间上不完全对齐。 由于它们的独立性，Prometheus 需要为每个相关时间序列在这些时间戳处分配一个值。 它通过简单地获取该时间戳之前的最新样本来实现这一点。

If a target scrape or rule evaluation no longer returns a sample for a time series that was previously present, that time series will be marked as stale. If a target is removed, its previously returned time series will be marked as stale soon afterwards.
如果目标抓取或规则评估不再返回先前存在的时间序列的样本，则该时间序列将被标记为过时。 如果删除目标，则其先前返回的时间序列将很快被标记为过时。

If a query is evaluated at a sampling timestamp after a time series is marked stale, then no value is returned for that time series. If new samples are subsequently ingested for that time series, they will be returned as normal.
如果在时间序列标记为过时后在采样时间戳处评估查询，则不会为该时间序列返回任何值。 如果随后在该时间序列中摄取新样本，它们将正常返回。

If no sample is found (by default) 5 minutes before a sampling timestamp, no value is returned for that time series at this point in time. This effectively means that time series "disappear" from graphs at times where their latest collected sample is older than 5 minutes or after they are marked stale.
如果在采样时间戳之前 5 分钟（默认情况下）未找到样本，则此时该时间序列不会返回任何值。 这实际上意味着，当最新收集的样本超过 5 分钟或被标记为过时后，时间序列就会从图表中“消失”。

Staleness will not be marked for time series that have timestamps included in their scrapes. Only the 5 minute threshold will be applied in that case.
对于抓取中包含时间戳的时间序列，不会标记过时性。 在这种情况下，仅应用 5 分钟阈值。

### Avoiding slow queries and overloads
避免缓慢的查询和过载

If a query needs to operate on a very large amount of data, graphing it might time out or overload the server or browser. Thus, when constructing queries over unknown data, always start building the query in the tabular view of Prometheus's expression browser until the result set seems reasonable (hundreds, not thousands, of time series at most). Only when you have filtered or aggregated your data sufficiently, switch to graph mode. If the expression still takes too long to graph ad-hoc, pre-record it via a [recording rule](https://prometheus.io/docs/prometheus/latest/configuration/recording_rules/#recording-rules).
如果查询需要对大量数据进行操作，则将其绘制图表可能会导致服务器或浏览器超时或过载。 因此，在构建对未知数据的查询时，始终在 Prometheus 表达式浏览器的表格视图中开始构建查询，直到结果集看起来合理（最多数百个而不是数千个时间序列）。 仅当您充分过滤或聚合数据后，才切换到图表模式。 如果表达式仍然需要太长的时间来绘制临时图表，请通过记录规则预先记录它。

This is especially relevant for Prometheus's query language, where a bare metric name selector like `api_http_requests_total` could expand to thousands of time series with different labels. Also keep in mind that expressions which aggregate over many time series will generate load on the server even if the output is only a small number of time series. This is similar to how it would be slow to sum all values of a column in a relational database, even if the output value is only a single number.
这与 Prometheus 的查询语言尤其相关，其中像 api_http_requests_total 这样的裸指标名称选择器可以扩展到数千个具有不同标签的时间序列。 另请记住，即使输出只是少量时间序列，聚合多个时间序列的表达式也会在服务器上产生负载。 这类似于对关系数据库中列的所有值求和的速度很慢，即使输出值只是一个数字。
