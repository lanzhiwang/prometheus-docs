# QUERY EXAMPLES

* https://prometheus.io/docs/prometheus/latest/querying/examples/

## Simple time series selection

Return all time series with the metric `http_requests_total`:

```
http_requests_total
```

Return all time series with the metric `http_requests_total` and the given `job` and `handler` labels:

```
http_requests_total{job="apiserver", handler="/api/comments"}
```

Return a whole range of time (in this case 5 minutes up to the query time) for the same vector, making it a [range vector](https://prometheus.io/docs/prometheus/latest/querying/basics/#range-vector-selectors):
返回同一向量的整个时间范围（在本例中为查询时间之前的 5 分钟），使其成为范围向量：

```
http_requests_total{job="apiserver", handler="/api/comments"}[5m]
```

Note that an expression resulting in a range vector cannot be graphed directly, but viewed in the tabular ("Console") view of the expression browser.

Using regular expressions, you could select time series only for jobs whose name match a certain pattern, in this case, all jobs that end with `server`:

```
http_requests_total{job=~".*server"}
```

All regular expressions in Prometheus use [RE2 syntax](https://github.com/google/re2/wiki/Syntax).

To select all HTTP status codes except 4xx ones, you could run:

```
http_requests_total{status!~"4.."}
```

```
http_requests_total[30m:1m]
```

Prometheus PromQL 查询语句 `http_requests_total[30m:1m]` 的含义是选择一个时间范围内的数据点，并以特定的时间间隔进行抽样。让我为您解释这个查询语句的各个部分：

1. `http_requests_total` 是一个时间序列，它表示在某个时间点之前接收到的 HTTP 请求总数。

2. `[30m:1m]` 是范围选择和抽样的部分。这部分的含义是选择在过去30分钟内的数据点，并以1分钟的间隔进行抽样。

具体来说，这个查询会返回一个包含过去30分钟内每隔1分钟的时间序列数据点的子集。这意味着您将获得过去30分钟内每分钟的 HTTP 请求总数数据点，以便对这些数据进行进一步的分析或可视化。

## Subquery

Return the 5-minute rate of the `http_requests_total` metric for the past 30 minutes, with a resolution of 1 minute.

```
rate(http_requests_total[5m])[30m:1m]
```

This is an example of a nested subquery. The subquery for the `deriv` function uses the default resolution. Note that using subqueries unnecessarily is unwise.

```
max_over_time(deriv(rate(distance_covered_total[5s])[30s:5s])[10m:])
```

## Using functions, operators, etc.

Return the per-second rate for all time series with the `http_requests_total` metric name, as measured over the last 5 minutes:
返回过去 5 分钟内测量的具有 http_requests_total 指标名称的所有时间序列的每秒速率：

```
rate(http_requests_total[5m])
```

Assuming that the `http_requests_total` time series all have the labels `job` (fanout by job name) and `instance` (fanout by instance of the job), we might want to sum over the rate of all instances, so we get fewer output time series, but still preserve the `job` dimension:
假设 http_requests_total 时间序列都有标签 job（按作业名称扇出）和实例（按作业实例扇出），我们可能希望对所有实例的速率求和，因此我们得到的输出时间序列较少，但仍然 保留工作维度：

```
sum by (job) (
  rate(http_requests_total[5m])
)
```

If we have two different metrics with the same dimensional labels, we can apply binary operators to them and elements on both sides with the same label set will get matched and propagated to the output. For example, this expression returns the unused memory in MiB for every instance (on a fictional cluster scheduler exposing these metrics about the instances it runs):
如果我们有两个具有相同维度标签的不同指标，我们可以对它们应用二元运算符，并且两侧具有相同标签集的元素将被匹配并传播到输出。 例如，此表达式返回每个实例的未使用内存（以 MiB 为单位）（在虚构的集群调度程序上公开有关其运行的实例的这些指标）：

```
(instance_memory_limit_bytes - instance_memory_usage_bytes) / 1024 / 1024
```

The same expression, but summed by application, could be written like this:

```
sum by (app, proc) (
  instance_memory_limit_bytes - instance_memory_usage_bytes
) / 1024 / 1024
```

If the same fictional cluster scheduler exposed CPU usage metrics like the following for every instance:
如果同一个虚构的集群调度程序为每个实例公开如下所示的 CPU 使用率指标：

```
instance_cpu_time_ns{app="lion", proc="web", rev="34d0f99", env="prod", job="cluster-manager"}
instance_cpu_time_ns{app="elephant", proc="worker", rev="34d0f99", env="prod", job="cluster-manager"}
instance_cpu_time_ns{app="turtle", proc="api", rev="4d3a513", env="prod", job="cluster-manager"}
instance_cpu_time_ns{app="fox", proc="widget", rev="4d3a513", env="prod", job="cluster-manager"}
...
```

...we could get the top 3 CPU users grouped by application (`app`) and process type (`proc`) like this:

```
topk(3, sum by (app, proc) (rate(instance_cpu_time_ns[5m])))
```

Assuming this metric contains one time series per running instance, you could count the number of running instances per application like this:

```
count by (app) (instance_cpu_time_ns)
```
